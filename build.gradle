buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'checkstyle'

boolean fullbuild=true

version = "0.1"
group = "allout58.nbttrack" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "NBTTracker"

minecraft {
    version = "1.7.10-10.13.2.1277"
    runDir = "run"

    // replacing
    replace "@MC_VERSION@", project.minecraft.version
    replace "@VERSION@", project.version
}

if (System.getenv()['BUILD_NUMBER']) {
    version += "-jnks" + System.getenv()['BUILD_NUMBER']
}

if(System.getenv().DRONE_BUILD_NUMBER)
{
	version += "-droneio"+System.getenv().DRONE_BUILD_NUMBER
    fullbuild=false
}

dependencies {
    compile project(":OpenModsLib")
}

ext.fmlManifest = manifest {
    attributes("FMLCorePlugin": "allout58.mods.nbttrack.NBTTrackCorePlugin", "FMLCorePluginContainsFMLMod": "true")
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// change the name of my obfuscated jar
jar {
    classifier = 'universal'
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

// add a javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier = 'javadoc'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}


artifacts {
    if(fullbuild)
    {
        archives sourceJar
        archives deobfJar
        archives javadocJar
    }
}